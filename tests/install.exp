#!/usr/bin/env expect

set vm [lindex $argv 0];
set logfilepath [lindex $argv 1];
log_file -noappend "$logfilepath"
set timeout 600
set PID [spawn vm console "$vm"]
send_user "Spawned PID: $PID \n"

# boot serial
expect {
  "Press enter to boot" {
    sleep .5
    send "j"
  }

  timeout {
    send_user "Grub timeout\n"
    exit 1
  }
}


# ┌────────────────────TrueNAS 22.12.0 Console Setup──────────────────────┐
# │ ┌───────────────────────────────────────────────────────────────────┐ │
# │ │                        1  Install/Upgrade                         │ │
# │ │                        2  Shell                                   │ │
# │ │                        3  Reboot System                           │ │
# │ │                        4  Shutdown System                         │ │
# │ │                                                                   │ │
# │ │                                                                   │ │
# │ └───────────────────────────────────────────────────────────────────┘ │
# ├───────────────────────────────────────────────────────────────────────┤
# │                     <  OK  >           <Cancel>                       │
# └───────────────────────────────────────────────────────────────────────┘

expect {
  "Console Setup" {
    sleep .5
    send "1"
    send "\r"
  }

  timeout {
    send_user "Boot timeout\n"
    exit 1
  }
}

# ┌────────────────Choose destination media──────────────────┐
# │ Select one or more drives where TrueNAS should be        │
# │ installed (use arrow keys to navigate to the drive(s)    │
# │ for installation; select a drive with the spacebar).     │
# │ ┌──────────────────────────────────────────────────────┐ │
# │ │            [*] vtbd0  (null) -- 20.0 GiB             │ │
# │ │            [ ] vtbd1  (null) -- 50.0 GiB             │ │
# │ │            [ ] vtbd2  (null) -- 50.0 GiB             │ │
# │ └──────────────────────────────────────────────────────┘ │
# ├──────────────────────────────────────────────────────────┤
# │               <  OK  >        <Cancel>                   │
# └──────────────────────────────────────────────────────────┘


expect {
  "Choose destination media" {
    sleep .5
    send \040
    send "\r"
  }
}

# ┌─────────────────────────TrueNAS installation───────────────────────────┐
# │ WARNING:                                                               │
# │ - This will erase ALL paritions and data on ada0.                      │
# │ - You can't use ada0 for sharing data.                                 │
# │                                                                        │
# │ NOTE:                                                                  │
# │ - Installing on flash media is preferred to install on a               │
# │   hard drive                                                           │
# │                                                                        │
# │   Proceed with the installation?                                       │
# ├────────────────────────────────────────────────────────────────────────┤
# │                       < Yes >            < No  >                       │
# └────────────────────────────────────────────────────────────────────────┘

expect {
  "TrueNAS installation" {
    sleep .5
    send "Y"
    send "\r"
  }
}

# ┌────────────────────Web UI Authentication Method───────────────────────┐
# │ ┌───────────────────────────────────────────────────────────────────┐ │
# │ │                  1  Administrative user (admin)                   │ │
# │ │                  2  Root user (not recommended)                   │ │
# │ │                  3  Configure using Web UI                        │ │
# │ │                                                                   │ │
# │ │                                                                   │ │
# │ │                                                                   │ │
# │ └───────────────────────────────────────────────────────────────────┘ │
# ├───────────────────────────────────────────────────────────────────────┤
# │                     <  OK  >           <Cancel>                       │
# └───────────────────────────────────────────────────────────────────────┘

## Old SCALE installer support

# ┌────────────────────────────────────────────────┐
# │ Enter your root password; cancel for no root   │
# │ password                                       │
# │ ┌────────────────────────────────────────────┐ │
# │ │Password:          *******                  │ │
# | |Confirm Password:  *******                  │ │
# │ └────────────────────────────────────────────┘ │
# ├────────────────────────────────────────────────┤
# │           <  OK  >      <Cancel>               │
# └────────────────────────────────────────────────┘

expect {
  "Web UI Authentication Method" {
    sleep .5
    send "2"
    send "\r"
  }

  # Old SCALE installer support
  "Enter your root password;" {
    sleep .5
    send "testing"
    # Tab to next field
    send \011
    sleep .2
    send "testing"
    # Tab to submit/OK button
    send \011
    sleep .2
    # Submit/confirm password dialog
    send "O"
    send "\r"
  }
}

# ┌────────────────────────────────────────────────────────────────────┐
# │ Enter your root password. Cancel for cancelling installation       │
# │ ┌────────────────────────────────────────────────────────────────┐ │
# │ │         Password:                                              │ │
# │ │         Confirm Password:                                      │ │
# │ └────────────────────────────────────────────────────────────────┘ │
# │                                                                    │
# ├────────────────────────────────────────────────────────────────────┤
# │                   <  OK  >          <Cancel>                       │
# └────────────────────────────────────────────────────────────────────┘

expect {
  "Enter your root password." {
    sleep .5
    send "testing"
    # Tab to next field
    send \011
    sleep .2
    send "testing"
    # Tab to submit/OK button
    send \011
    sleep .2
    # Submit/confirm password dialog
    send "O"
    send "\r"
  }

  # old SCALE installer support
  "installation on sda succeeded!" {
    send_user "Bhyve installation finished\n"
    sleep 5
    exit 0
  }

  "installation on sda failed." {
    send_user "Bhyve installation failed\n"
    sleep 5
    exit 1
  }

  "has failed. Press enter to continue" {
    send_user "Bhyve installation failed\n"
    sleep 5
    exit 1
  }

  timeout {
    send_user "Installation timeout\n"
    exit 1
  }
}

# ┌────────────────────────────────────────────────────────────────────────┐
# │ The TrueNAS installation on sda succeeded!                             |
# │                                                                        │
# ├────────────────────────────────────────────────────────────────────────┤
# │                               <  OK  >                                 │
# └────────────────────────────────────────────────────────────────────────┘

# Or

# ┌────────────────────────────────────────────────────────────────────────┐
# │ The TrueNAS installation on sda failed.                                |
# │                                                                        │
# ├────────────────────────────────────────────────────────────────────────┤
# │                               <  OK  >                                 │
# └────────────────────────────────────────────────────────────────────────┘

# Installation complete!
expect {
  "installation on sda succeeded!" {
    send_user "Bhyve installation finished\n"
    sleep 5
    exit 0
  }

  "installation on sda failed." {
    send_user "Bhyve installation failed\n"
    sleep 5
    exit 1
  }

  "has failed. Press enter to continue" {
    send_user "Bhyve installation failed\n"
    sleep 5
    exit 1
  }

  timeout {
    send_user "Installation timeout\n"
    exit 1
  }
}
